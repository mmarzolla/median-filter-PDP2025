# gnuplot script to produce Figure 3 from the paper.
# The input data must be generated by test-cuda.sh.
#
# To execute this script:
#
#       gnuplot plot.gp
#
# Written in 2024 by Moreno Marzolla.
# Last modified on 2025-01-17 by Moreno Marzolla.

set term postscript eps mono font ",18"
set output "results.eps"
set logscale y
set key bottom right
set xlabel "Radius R"
set ylabel "Execution time (s)"

set multiplot layout 1, 2
set title "16 bpp"
plot [][:10000] \
     "test-cuda-hist-generic-X1024-B16.txt" with lp lt 1 lw 3 pt 7 ps 1.5 notitle, \
     "test-cuda-hist-generic-X2048-B16.txt" with lp lt 1 lw 3 pt 4 ps 1.5 notitle, \
     "test-cuda-hist-generic-X4096-B16.txt" with lp lt 1 lw 3 pt 2 ps 1.5 notitle, \
     "test-omp-hist-sparse-byrow-X1024-B16.txt" with lp lt 2 lw 3 pt 7 ps 1.5 notitle, \
     "test-omp-hist-sparse-byrow-X2048-B16.txt" with lp lt 2 lw 3 pt 4 ps 1.5 notitle, \
     "test-omp-hist-sparse-byrow-X4096-B16.txt" with lp lt 2 lw 3 pt 2 ps 1.5 notitle
set title "32 bpp"
unset ylabel
plot [][:10000] \
     "test-cuda-hist-generic-X1024-B32.txt" with lp lt 1 lw 3 pt 7 ps 1.5 notitle, \
     "test-cuda-hist-generic-X2048-B32.txt" with lp lt 1 lw 3 pt 4 ps 1.5 notitle, \
     "test-cuda-hist-generic-X4096-B32.txt" with lp lt 1 lw 3 pt 2 ps 1.5 notitle, \
     "test-omp-hist-sparse-byrow-X1024-B32.txt" with lp lt 2 lw 3 pt 7 ps 1.5 notitle, \
     "test-omp-hist-sparse-byrow-X2048-B32.txt" with lp lt 2 lw 3 pt 4 ps 1.5 notitle, \
     "test-omp-hist-sparse-byrow-X4096-B32.txt" with lp lt 2 lw 3 pt 2 ps 1.5 notitle , \
     NaN with l lw 3 lt 1 title "CUDA", \
     NaN with l lw 3 lt 2 title "OpenMP", \
     NaN with p pt 7 lw 3 ps 1.5 title "1024x1024", \
     NaN with p pt 4 lw 3 ps 1.5 title "2048x2048", \
     NaN with p pt 2 lw 3 ps 1.5 title "4096x4096"
unset multiplot